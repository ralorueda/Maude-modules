mod NARROWING-VENDING-MACHINE is
   sorts Coin Item Marking Money State .
   subsort Coin < Money .
   op empty : -> Money .
   op __ : Money Money -> Money [assoc comm id: empty] .
   subsort Money Item < Marking .
   op __ : Marking Marking -> Marking [assoc comm id: empty] .
   op <_> : Marking -> State .
   ops $ q : -> Coin .
   ops c a : -> Item .
   var M : Marking .
   rl [buy-c] : < M $ > => < M c > [narrowing] .
   rl [buy-t] : < M $ > => < M a q > [narrowing] .
   eq [change] : q q q q M = $ M [variant] .
endm


fmod META-NARROWING-APPLY-BOUNDED is
    protecting META-LEVEL .
    protecting BOOL .

    sorts Node .
    subsort Nat < Node .

    sorts NarrowingApplyResultList NarrowingApplyResultStructure State .
    subsort  NarrowingApplyResultStructure < NarrowingApplyResultList < State . 
    
    var M : Module .   var T : Term .   var TList : TermList .   var Q1 Q2 : Qid .   var N : Nat .
    var Ty : Type .   var C : Context .   vars S1 S2 : Substitution .   var NAR : NarrowingApplyResult .
    var Nod : Node .   vars MaxDepth MaxSol : Bound .   vars NARS1 NARS2 : NarrowingApplyResultList .

    op initNode : -> Node .
    op _._ : Node Node -> Node [assoc id: initNode] .
    op depth : Node -> Nat .
    
    eq depth(initNode) = 0 .
    eq depth(Nod . N) = depth(Nod) + 1 .

    op metaNarrowingApplyBounded : Module Term TermList Qid Bound Bound -> State .
    op metaNarrowingApplyUnfold : Module Term TermList Qid Node -> NarrowingApplyResultList .
    op nil : -> NarrowingApplyResultList [ctor] .
    op _;_ : NarrowingApplyResultList NarrowingApplyResultList -> NarrowingApplyResultList [assoc id: nil] .
    op {_,_} : Node NarrowingApplyResult -> NarrowingApplyResultStructure .
    op (_,_,_,_,_,_) : Module TermList NarrowingApplyResultList NarrowingApplyResultList Bound Bound ->  State .

    eq metaNarrowingApplyBounded(M, T, TList, Q1, 0, MaxSol:NzNat) = nil .
    eq metaNarrowingApplyBounded(M, T, TList, Q1, MaxDepth:NzNat, 0) = nil .
    eq metaNarrowingApplyBounded(M, T, TList, Q1, MaxDepth:NzNat, MaxSol:NzNat)
       = (M, TList, nil, {0, metaNarrowingApply(M, T, TList, Q1, 0)}, MaxDepth:NzNat, MaxSol:NzNat) . 
    

    eq (M, TList, NARS1, nil, MaxDepth, MaxSol:NzNat) = NARS1 .
    eq (M, TList, NARS1, NARS2, MaxDepth, 0) = NARS1 .
    eq (M, TList, NARS1, {Nod . N, failure} ; NARS2, MaxDepth, MaxSol:NzNat) = (M, TList, NARS1, NARS2, MaxDepth, MaxSol:NzNat) .

    eq (M, TList, NARS1, {Nod . N, {T, Ty, C, Q1, S1, S2, Q2}} ; NARS2, unbounded, unbounded)
        = (M, TList, NARS1 ; {Nod . N, {T, Ty, C, Q1, S1, S2, Q2}}, {Nod . N + 1, metaNarrowingApply(M, T, TList, Q2, N + 1)} ; NARS2 ;
          {Nod . N . 0, metaNarrowingApply(M, getTerm({T, Ty, C, Q1, S1, S2, Q2}), TList, Q2, 0)}, unbounded, unbounded) .

    eq (M, TList, NARS1, {Nod . N, {T, Ty, C, Q1, S1, S2, Q2}} ; NARS2, unbounded, MaxSol:Nat)
        = (M, TList, NARS1 ; {Nod . N, {T, Ty, C, Q1, S1, S2, Q2}}, {Nod . N + 1, metaNarrowingApply(M, T, TList, Q2, N + 1)} ; NARS2 ;
          {Nod . N . 0, metaNarrowingApply(M, getTerm({T, Ty, C, Q1, S1, S2, Q2}), TList, Q2, 0)}, unbounded, sd(MaxSol:Nat,1)) .

    eq (M, TList, NARS1, {Nod . N, {T, Ty, C, Q1, S1, S2, Q2}} ; NARS2, MaxDepth:Nat, unbounded)
        = if depth(Nod . N) <= MaxDepth:Nat then
                  (M, TList, NARS1 ; {Nod . N, {T, Ty, C, Q1, S1, S2, Q2}}, {Nod . N + 1, metaNarrowingApply(M, T, TList, Q2, N + 1)} ; NARS2 ;
                  {Nod . N . 0, metaNarrowingApply(M, getTerm({T, Ty, C, Q1, S1, S2, Q2}), TList, Q2, 0)}, MaxDepth:Nat, unbounded) 
          else NARS1 fi .

    eq (M, TList, NARS1, {Nod . N, {T, Ty, C, Q1, S1, S2, Q2}} ; NARS2, MaxDepth:Nat, MaxSol:Nat)
        = if depth(Nod . N) <= MaxDepth:Nat then
                  (M, TList, NARS1 ; {Nod . N, {T, Ty, C, Q1, S1, S2, Q2}}, {Nod . N + 1, metaNarrowingApply(M, T, TList, Q2, N + 1)} ; NARS2 ;
                  {Nod . N . 0, metaNarrowingApply(M, getTerm({T, Ty, C, Q1, S1, S2, Q2}), TList, Q2, 0)}, MaxDepth:Nat, sd(MaxSol:Nat,1)) 
          else NARS1 fi .

endfm

reduce in META-NARROWING-APPLY-BOUNDED :
 metaNarrowingApplyBounded(upModule('NARROWING-VENDING-MACHINE, false),
              '<_>['M:Money], empty, '@, 4, 15) .

