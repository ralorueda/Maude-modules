mod NARROWING-VENDING-MACHINE is
   sorts Coin Item Marking Money State .
   subsort Coin < Money .
   op empty : -> Money .
   op __ : Money Money -> Money [assoc comm id: empty] .
   subsort Money Item < Marking .
   op __ : Marking Marking -> Marking [assoc comm id: empty] .
   op <_> : Marking -> State .
   ops $ q : -> Coin .
   ops c a : -> Item .
   var M : Marking .
   rl [buy-c] : < M $ > => < M c > [narrowing] .
   rl [buy-t] : < M $ > => < M a q > [narrowing] .
   eq [change] : q q q q M = $ M [variant] .
endm


fmod CONVERT-VARIABLES is
    pr META-LEVEL .
    pr CONVERSION .

    var F : Qid .
    var V : Variable .
    var GT : GroundTerm .
    var GNTL : NeGroundTermList .
    var NTL : NeTermList .
    var T : Term .
    var N : Nat .
    vars TL TL' TL'' : TermList .
    vars SB SB' : Substitution .

    op dollarize : Term -> Term .
    eq dollarize(GT) = GT .
    eq dollarize(V) =
        if ((substr(string(V),0,1) == "#")
        or-else (substr(string(V),0,1) == "@")
        or-else ((substr(string(V),0,1) == "%")))
        then qid("$" + substr(string(V),1,length(string(V)) + -1))
        else V
        fi .
    eq dollarize(F[NTL]) = F[dollarize(NTL)] [owise] .

    op dollarize : TermList -> TermList .
    eq dollarize(empty)= empty .
    eq dollarize((T,GNTL)) = (dollarize(T),GNTL) .
    eq dollarize((T,NTL)) = (dollarize(T),dollarize(NTL)) .

    op rename : Term -> Term .
    eq rename(GT) = GT .
    eq rename(T) = applySub(T,rename'(getVars(T),1)) [owise] .

    op rename' : TermList Nat -> Substitution .
    eq rename'(empty,N) = none .
    eq rename'((TL,V,TL',V,TL''),N) = rename'((TL,V,TL',TL''),N) .
    eq rename'((V,TL),N) = V <- qid("$" + string(N,10) + ":" +
    string(getType(V))) ; rename'(TL,N + 1) [owise] .

    op getVars : Term -> TermList .
    eq getVars(GT) = empty .
    eq getVars(V) = V .
    eq getVars(F[NTL]) = getVars(NTL) [owise] .

    op getVars : TermList -> TermList .
    eq getVars(empty) = empty .
    eq getVars((T,GNTL)) = getVars(T) .
    eq getVars((T,NTL)) = (getVars(T),getVars(NTL)) [owise] .

    op applySub : TermList Substitution -> TermList .
    eq applySub(V,(V <- T) ; SB) = T .
    eq applySub(F[TL], SB) = F[applySub(TL, SB)] .
    eq applySub((T,NTL),SB) = (applySub(T,SB), applySub(NTL,SB)) .
    eq applySub(T,SB) = T [owise] .

    op applySub : Substitution Substitution -> Substitution .
    eq applySub((none).Substitution,SB) = none .
    eq applySub(V <- T ; SB,SB') =  V <- applySub(T,SB') ; applySub(SB,SB') .
endfm


fmod META-NARROWING-SEARCH-BOUNDED is
    protecting META-LEVEL .
    protecting BOOL .
    protecting CONVERT-VARIABLES .

    sorts Node .
    subsort Nat < Node .

    sorts NarrowingSearchResultList NarrowingSearchResultStructure .
    subsort NarrowingSearchResultStructure < NarrowingSearchResultList . 

    var M : Module .   vars T1 T2 : Term .   var TLIST : TermList .   var Q1 Q2 Q3 : Qid .   var N : Nat .
    var TY : Type .   var C : Context .   vars S1 S2 : Substitution .   var NAR : NarrowingApplyResult .
    var Nod : Node .   vars MAXDEPTH MAXSOL : Bound .   vars NARS1 NARS2 : NarrowingSearchResultList .

    op initNode : -> Node .
    op _._ : Node Node -> Node [assoc id: initNode] .
    op depth : Node -> Nat .
    
    eq depth(initNode) = 0 .
    eq depth(Nod . N) = depth(Nod) + 1 .

    op metaNarrowingSearchBounded : Module Term TermList Qid Bound Bound -> NarrowingSearchResultList .
    op nil : -> NarrowingSearchResultList [ctor] .
    op _;_ : NarrowingSearchResultList NarrowingSearchResultList -> NarrowingSearchResultList [assoc id: nil] .
    op {_,_,_,_} : Node  Term Qid NarrowingApplyResult -> NarrowingSearchResultStructure .
    op (_,_,_,_,_,_) : Module TermList NarrowingSearchResultList NarrowingSearchResultList Bound Bound ->  NarrowingSearchResultList .
    op [_] : NarrowingSearchResultList -> NarrowingSearchResultList .



    eq metaNarrowingSearchBounded(M, T2, TLIST, Q1, 0, MAXSOL) = [nil] .
    eq metaNarrowingSearchBounded(M, T2, TLIST, Q1, MAXDEPTH, 0) = [nil] .
    eq metaNarrowingSearchBounded(M, T2, TLIST, Q1, MAXDEPTH, MAXSOL) =
        (M, TLIST, nil, {0, T2, Q1, metaNarrowingApply(M, T2, TLIST, Q1, 0)}, MAXDEPTH, MAXSOL) [owise] .
    
    eq (M, TLIST, NARS1, NARS2, MAXDEPTH, 0) = [NARS1] .
    eq (M, TLIST, NARS1, nil, MAXDEPTH, MAXSOL) = [NARS1] .
    eq (M, TLIST, NARS1, {Nod . N, T1, Q1, failure} ; NARS2, MAXDEPTH, MAXSOL) = (M, TLIST, NARS1, NARS2, MAXDEPTH, MAXSOL) .

    eq (M, TLIST, NARS1, {Nod . N, T1, Q3, {T2, TY, C, Q1, S1, S2, Q2}} ; NARS2, unbounded, unbounded)
        = (M, TLIST, NARS1 ; {Nod . N, T1, Q3, {T2, TY, C, Q1, S1, S2, Q2}}, {Nod . N + 1, T1, Q3, metaNarrowingApply(M, T1, TLIST, Q3, N + 1)} ; NARS2 ;
          {Nod . N . 0, T2, Q2, metaNarrowingApply(M, T2, TLIST, Q2, 0)}, unbounded, unbounded) .

    eq (M, TLIST, NARS1, {Nod . N, T1, Q3, {T2, TY, C, Q1, S1, S2, Q2}} ; NARS2, unbounded, MAXSOL:NzNat)
        = (M, TLIST, NARS1 ; {Nod . N, T1, Q3, {T2, TY, C, Q1, S1, S2, Q2}}, {Nod . N + 1, T1, Q3, metaNarrowingApply(M,  T1, TLIST, Q3, N + 1)} ; NARS2 ;
          {Nod . N . 0, T2, Q2, metaNarrowingApply(M, T2, TLIST, Q2, 0)}, unbounded, sd(MAXSOL:NzNat,1)) .

    eq (M, TLIST, NARS1, {Nod . N, T1, Q3, {T2, TY, C, Q1, S1, S2, Q2}} ; NARS2, MAXDEPTH:NzNat, unbounded)
        = if depth(Nod . N) <= MAXDEPTH:NzNat then
                  (M, TLIST, NARS1 ; {Nod . N, T1, Q3, {T2, TY, C, Q1, S1, S2, Q2}}, {Nod . N + 1, T1, Q3, metaNarrowingApply(M,  T1, TLIST, Q3, N + 1)} ; NARS2 ;
                  {Nod . N . 0, T2, Q2, metaNarrowingApply(M, T2, TLIST, Q2, 0)}, MAXDEPTH:NzNat, unbounded) 
          else [NARS1] fi .

    eq (M, TLIST, NARS1, {Nod . N, T1, Q3, {T2, TY, C, Q1, S1, S2, Q2}} ; NARS2, MAXDEPTH:NzNat, MAXSOL:NzNat)
        = if depth(Nod . N) <= MAXDEPTH:NzNat then
                  (M, TLIST, NARS1 ; {Nod . N, T1, Q3, {T2, TY, C, Q1, S1, S2, Q2}}, {Nod . N + 1, T1, Q3, metaNarrowingApply(M,  T1, TLIST, Q3, N + 1)} ; NARS2 ;
                  {Nod . N . 0, T2, Q2, metaNarrowingApply(M, T2, TLIST, Q2, 0)}, MAXDEPTH:NzNat, sd(MAXSOL:NzNat,1)) 
          else [NARS1] fi .

    eq [nil] = nil .
    eq [{Nod . N, T1, Q3, {T2, TY, C, Q1, S1, S2, Q2}} ; NARS1] = {Nod . N, T1, Q3, {T2, TY, C, Q1, S1, S2, Q2}} ; [NARS1] .
endfm


set show advisories on .

reduce in META-NARROWING-SEARCH-BOUNDED :
 metaNarrowingSearchBounded(upModule('NARROWING-VENDING-MACHINE, false),
              '<_>['@1:Money], empty, '@, 3, 5) .


--- Qid, String (Transormar Qid a String y viceversa)
--- ... <<<